buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/groups/public/' }
	}
	dependencies {
		classpath 'org.ajoberstar:gradle-git:0.3.0'
		classpath 'org.ajoberstar:gradle-jacoco:0.2.0'
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.5.0'
	}
}

allprojects {
	apply plugin: 'groovy'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'jacoco'
	apply from: rootProject.file('config/publishing.gradle')
	apply from: rootProject.file('config/license.gradle')

	group = 'org.ajoberstar'

	sourceCompatibility = '1.5'

	repositories { mavenCentral() }

	dependencies {
		groovy localGroovy()
		testCompile 'junit:junit:4.10'
		testCompile('org.spockframework:spock-core:0.7-groovy-1.8') { exclude group: 'org.codehaus.groovy' }
	}
}

apply plugin: 'github-pages'

description = 'Git plugins for Gradle'
version = '0.6.0-SNAPSHOT'

dependencies {
	compile gradleApi()
	compile project(':grgit')
	compile 'org.eclipse.jgit:org.eclipse.jgit:2.2.0.201212191850-r'
	compile 'org.eclipse.jgit:org.eclipse.jgit.ui:2.2.0.201212191850-r'
}

githubPages {
	repoUri = 'git@github.com:ajoberstar/gradle-git.git'
	pages {
		from 'src/gh-pages'
		from(javadoc.outputs.files) {
			into 'docs/javadoc'
		}
		from(groovydoc.outputs.files) {
			into 'docs/groovydoc'
		}
	}
}
