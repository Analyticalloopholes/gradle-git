buildscript {
	repositories {
		mavenLocal()
		jcenter()
		mavenCentral()
		maven { url = 'http://oss.jfrog.org/artifactory/simple/oss-snapshot-local/' }
	}
	dependencies {
		// classpath 'org.ajoberstar:gradle-git:0.7.0-SNAPSHOT'
		classpath 'org.ajoberstar:gradle-git:0.6.1'
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.6.0'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
	}
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'github-pages'
apply from: rootProject.file('gradle/publishing.gradle')
apply from: rootProject.file('gradle/license.gradle')

group = 'org.ajoberstar'
description = 'Git plugins for Gradle'
version = '0.7.0-SNAPSHOT'

sourceCompatibility = '1.5'

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	compile localGroovy()
	compile gradleApi()
	compile 'org.ajoberstar:grgit:0.1.0'

	testCompile 'junit:junit:4.10'
	testCompile('org.spockframework:spock-core:0.7-groovy-1.8') { exclude group: 'org.codehaus.groovy' }
}

task sourcesJar(type:Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
	classifier = 'javadoc'
	from javadoc.outputs.files
}

task groovydocJar(type:Jar) {
	classifier = 'groovydoc'
	from groovydoc.outputs.files
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives groovydocJar
}

githubPages {
	repoUri = 'git@github.com:ajoberstar/gradle-git.git'
	pages {
		from 'src/gh-pages'
		from(javadoc.outputs.files) {
			into 'docs/javadoc'
		}
		from(groovydoc.outputs.files) {
			into 'docs/groovydoc'
		}
	}
}

wrapper {
	gradleVersion = '1.8'
}
