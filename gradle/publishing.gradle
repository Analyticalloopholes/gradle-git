apply plugin: 'maven-publish'
apply plugin: 'bintray'

publishing {
	publications {
		main(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			artifact groovydocJar

			pom.withXml {
				asNode().with {
					appendNode('description', 'Git plugins for Gradle')
					appendNode('url', 'http://github.com/ajoberstar/gradle-git')
					appendNode('licenses').with {
						appendNode('license').with {
							appendNode('name', 'The Apache Software License, Version 2.0')
							appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0')
						}
					}
					appendNode('developers').with {
						appendNode('developer').with {
							appendNode('id', 'ajoberstar')
							appendNode('name', 'Andrew Oberstar')
							appendNode('email', 'andrew@ajoberstar.org')
						}
					}
					appendNode('scm').with {
						appendNode('connection', 'git://github.com/ajoberstar/gradle-git.git')
						appendNode('developerConnection', 'git@github.com:ajoberstar/gradle-git.git')
						appendNode('url', 'http://github.com/ajoberstar/gradle-git')
					}
				}
			}
		}
	}
	repositories {
		mavenLocal()
		if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
			maven {
				name = 'JfrogSnapshots'
				url = 'http://oss.jfrog.org/artifactory/simple/oss-snapshot-local/'
				credentials {
					username = project.bintrayUser
					password = project.bintrayKey
				}
			}
		}
	}
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
	bintray {
		user = project.bintrayUser
		key = project.bintrayKey
		publications = ['main']
		pkg {
			repo = 'gradle-plugins'
			name = 'ajoberstar:gradle-git'
			desc = 'Git plugins for Gradle'
			licenses = ['Apache-2.0']
			labels = ['gradle', 'git']
		}
	}
}
